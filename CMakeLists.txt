cmake_minimum_required( VERSION 3.17 FATAL_ERROR )

list( APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/external/ecbuild/cmake" )
find_package( ecbuild REQUIRED )

project( DALES VERSION 5.0.0 LANGUAGES Fortran )

# Options

ecbuild_add_option( FEATURE FFTW
                    DESCRIPTION "Build with FFTW"
                    DEFAULT ON
                    REQUIRED_PACKAGES "NAME FFTW COMPONENTS single double" )

ecbuild_add_option( FEATURE HYPRE
                    DESCRIPTION "Build with HYPRE"
                    DEFAULT OFF )

ecbuild_add_option( FEATURE FP32_FIELDS
                    DESCRIPTION "Use single precision for fields"
                    DEFAULT OFF )

ecbuild_add_option( FEATURE FP32_POIS
                    DESCRIPTION "Use single precision for Poisson solver"
                    DEFAULT OFF )

ecbuild_add_option( FEATURE NVTX 
                    DESCRIPTION "Enable NVTX profiling (only available when using NVIDIA compiler)"
                    CONDITION "${CMAKE_Fortran_COMPILER_ID}" STREQUAL "NVHPC"
                    DEFAULT OFF )

ecbuild_add_option( FEATURE DALESLIB 
                    DESCRIPTION "Build DALES libraries for use with OMUSE"
                    DEFAULT OFF )

# Check for required dependencies

ecbuild_find_package( NetCDF REQUIRED COMPONENTS Fortran )
ecbuild_find_mpi( COMPONENTS Fortran REQUIRED )

# Set precision

if( ENABLE_FP32_FIELDS )
  set ( FIELD_PRECISION 32 )
else()
  set ( FIELD_PRECISION 64 )
endif()

add_compile_definitions( FIELD_PRECISION=${FIELD_PRECISION} )

if( ENABLE_FP32_POIS )
  set ( POIS_PRECISION 32 )
else()
  set ( POIS_PRECISION 64 )
endif()

add_compile_definitions( POIS_PRECISION=${POIS_PRECISION} )

# Additional options

if( ENABLE_FFTW )
  add_compile_definitions( USE_FFTW )
endif()

if( ENABLE_HYPRE )
  # For some reason, ecbuild_find_package doesn't find HYPRE (on Mac, at least)
  find_library( HYPRE_LIB HYPRE REQUIRED )
  ecbuild_info( "Found HYPRE: ${HYPRE_LIB}" )
  add_compile_definitions( USE_HYPRE )
endif()

if( ENABLE_NVTX )
  ecbuild_add_fortran_flags( -lnvToolsExt )
  add_compile_definitions( USE_NVTX )
endif()

add_subdirectory( src )